# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")

action("aggregate_vector_icons") {
  visibility = [ ":*" ]

  script = "aggregate_vector_icons.py"

  icons = [
    "account_box.icon",
    "account_child.icon",
    "account_child_circle.icon",
    "account_circle.icon",
    "apps.icon",
    "bar_close.1x.icon",
    "bar_close.icon",
    "blocked_badge.icon",
    "business.icon",
    "check_circle.1x.icon",
    "check_circle.icon",
    "close_all.icon",
    "code.icon",
    "cookie.icon",
    "default_favicon.icon",
    "eol.icon",
    "error_circle.icon",
    "extension.icon",
    "extension_crashed.icon",
    "file_download.icon",
    "file_download_incognito.1x.icon",
    "file_download_incognito.icon",
    "file_download_shelf.icon",
    "fullscreen.icon",
    "globe.icon",
    "help_outline.icon",
    "image.icon",
    "info_outline.icon",
    "location_on.icon",
    "lock.icon",
    "media_router_active.icon",
    "media_router_error.icon",
    "media_router_idle.icon",
    "media_router_warning.icon",
    "microphone.icon",
    "midi.icon",
    "mixed_content.icon",
    "mode_edit.icon",
    "my_location.icon",
    "notifications.icon",
    "notifications_off.icon",

    # TODO(estade): this is the same as the one in components/omnibox, but it's
    # referenced from Ash. De-dupe this soon.
    "omnibox_search.icon",
    "paintbrush.icon",
    "pdf.1x.icon",
    "pdf.icon",
    "photo_camera.icon",
    "protocol_handler.icon",
    "remove_box.icon",
    "remove_circle.icon",
    "sad_tab.icon",
    "settings.icon",
    "subresource_filter_active.icon",
    "supervisor_account.icon",
    "supervisor_account_circle.icon",
    "sync_problem.icon",
    "user_account_avatar.icon",
    "videocam.icon",
    "warning.icon",
    "warning_badge.icon",
    "web.icon",
    "${branding_path_component}/product.icon",
  ]

  output_cc = "$target_gen_dir/vector_icons.cc"
  output_h = "$target_gen_dir/vector_icons.h"

  inputs = icons
  inputs += [
    "vector_icons.cc.template",
    "vector_icons.h.template",
  ]
  outputs = [
    output_cc,
    output_h,
  ]

  response_file_contents = rebase_path(icons, root_build_dir)

  args = [
    "--working_directory=" + rebase_path("./"),
    "--file_list={{response_file_name}}",
    "--output_cc=" + rebase_path(output_cc, root_build_dir),
    "--output_h=" + rebase_path(output_h, root_build_dir),
    "--use_legacy_template",
  ]
}

source_set("vector_icons") {
  sources = get_target_outputs(":aggregate_vector_icons")
  sources += [
    "../vector_icon_types.h",
    "../vector_icons_public.h",
  ]

  deps = [
    ":aggregate_vector_icons",
    "//base",
    "//skia",
  ]
}
